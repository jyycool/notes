# 04 | 构建脚本概要

在第二部分将进一步提高知识. 我们将着眼于更高级的主题, 比如依赖管理、使用 Gradle 对应用程序进行测试、使用插件来扩展构建、以及更多的内容.

本章第四章是 Gradle 构建模块的精髓构建块. 你hi学到如何申明新的 task, 操作已有的 task, 为复杂的逻辑实现合适的抽象.

本章涵盖

- Gradle 的构建块及其 API 表示
- 声明 task 和操作 task
- task 的高级用法
- 实现和使用task 类型
- 了解构建生命周期挂接

本章中我们探索 Gradle 最基本的构建块, 也就是 project 和 task, 以及它们是如何映射到 Gradle 的 API 中的类上的. 这些类通过方法暴露了很多属性, 从而帮助控制构建. 我们将学到如何通过属性来控制构建的行为, 以及结构化构建逻辑的好处等.

## 4.1 构建块

每个 Gradle 都包含了 3 个基本构建块: project, task 和 property. 每个构建至少包含一个 project, 进而又包含一个或多个task. project 和 task 暴露的属性都可以用来控制构建.



### 4.1.1 项目

在 Gradle 术语中, ***一个项目(project) 代表一个正在构建的组件(比如, 一个 JAR 文件), 或一个想要完成的目标, 如部署应用等***. Gradle 的 build.gradle 文件相当于 maven 的 pom.xml. build.gradle 也被称为构建脚本. 每个 Gradle 构建脚本至少定义一个项目. 当构建进程启动后, gradle 基于build.gradle 中的配置实例化 org.gradle.api.Project 类, 并且可以通过 project 变量使其隐式可用.

下面是其 API 接口及其最终要的方法

```groovy
<<interface>>
project
------------------------------------
// 构建脚本配置
apply(options: Map<String, ?>
buildscript(config: Clousre)

// 依赖配置管理
dependencies(config: Closure)
configurations(config: Closure)
getDependencies()
getConfigurations()

// getter/setter 属性      
getAnt()
getName()
getDescription()
getGroup()
getPath()
getVersion()
getLogger()
setDescription(description: String)
setVersion(version: Object)

// 创建文件      
file(path: Object)
files(paths: Object)
fileTree(baseDir: Object)

// 创建 task
task(args: Map<String, ?>, name: String)
task(args: Map<String, ?>, name: String, c: Closure)
task(name: String)
task(name: String, c: Closure)
```

一个 project  可以创建新的 task, 添加依赖关系和配置, 并应用插件和其他的构建脚本. 它的许多属性, 如 name 和 descrotpion, 可以通过 getter/setter 方法访问

在构建 project 实例中, 你可以通过代码来访问 Gradle 的所有属性, 比如 task 的创建和以来的管理. ***记住, 当访问属性和方法的时候不需要使用 project 变量*** -- 它会假设你是指 Project 实例.

```groovy
// 不显示的调用 project 变量的情况下设置项目描述
setDescription("myProject")
// 不使用 project 变量的情况下通过 Groovy 语法来访问 name 和 description 属性
println "Description of project $name:" + project.description
```

多模块项目中, 每一个模块都将被表示为一个 Gradle 项目, 并且拥有自己独立的 build.gradle 脚本, 这些模块可以相互依赖. 第六章完全致力于使用 Gradle 进行多模块项目的构建.

接下来我们将看看 task 特点, task 是 gradle 的另一个核心构建块.



### 4.1.2 任务

task 的一些重要的功能: 任务动作 (action) 和任务依赖 (task dependency).

任务动作定义了一个当任务执行时最小的工作单元.很多时候,运行一个 task 之前需要运行另一个 task, 尤其当 task 的运行需要以另一个 task 的输出作为输入来完成自己的行动时更是如此. task 的 API 表示, org.gradle.api.Task

下面是其 API 接口及其最终要的方法

```groovy
<<interface>>
Task
------------------------------------
// task 依赖
dependsOn(tasks: Object...)

// 动作定义
doFirst(action: Closure)
doLast(action: Closure)
getActions()

// 输入/输出数据声明
getInputs()
getOutputs()

// getter/setter 属性
getAnt()
getDescription()
getEnabled()
getGroup()
setDescription(description: String)
setEnabled(enabled: boolean)
setGroup(group: String)
```

每个 Project 和 Task 实例都提供了可以通过 getter/setter 方法访问的属性. 一个属性可能是一个任务的描述或项目的版本.

通常, 你需要定义自己的属性. 比如, 你可能想要申明一个变量, 该变量引用了同样一个构建脚本中多次使用的一个文件, Gradle 允许用户通过扩展属性自定义一些变量

#### 扩展属性

Gradle 很多领域模型类提供了特别的属性支持. 在内部, 这些书写都以键值对的形式存储.

为了添加属性, 你需要使用 ext 命名空间. 下面代码演示以不同的方式添加、读取和修改一个属性

```groovy
// 只在初始申明扩展属性是需要使用 ext 命名空间
project.ext.myProp = 'myValue'
ext{
	someOtherProp = 123
}

// 使用 ext 命名空间访问属性是可选的
assert myProp == 'myValue'
println project.someOtherProp
ext.someOtherProp = 567
```

类似的, 额外的属性也可以通过属性文件来提供

#### Gradle 属性

gradle 属性可以通过在 gradle.properties 文件中申明直接添加到项目中, 这文件位于`<USER_HOME>/.gradle` 目录或项目的根目录下. 这些属性可以通过项目实例访问. 记住, 即使你有多个项目, 每一个用户也只能有一个 Gradle 属性文件在 `<USER_HOME>/.gradle` 目录下. 这是目前 Gradle 对它的限制. 这个属性文件中声明的属性对所有项目可用. 例如我们在 gradle.properties 文件中声明了如下属性

```groovy
examplePro = myValue
someOtherProp = 345
```

你可以按照如下方式访问项目中的这两个变量:

```groovy
assert project.example == 'myValue'

task printGradleProperty {
	doLast{
		print "Second property: $someOtherProp"
	}
}
```

#### 声明属性的方式

gradle 特提供了除了上面自定义变量及其值的方式外的很多方式为构建提供属性, 例如:

- 项目属性通过 -P 命令行选项提供

- 系统属性通过 -D 命令行选项提供

- 环境属性按照下面的模式提供

  ```groovy
  ORG_GRADLE_PROJECT_propertyName = someValue
  ```



## 4.2 使用 task

默认情况下, 每一个新建的 task都是 `org.gradle.api.DefaultTask` 类型的, 他是标准的 `org.gradle.api.Task` 的实现. DefaultTask 中所有属性都是 private 的. 这意味着他们只能通过 getter/setter 来访问. 但 groovy 提供了一些语法糖, 可以直接使用属性名来使用属性.



### 4.2.1 项目版本管理

通常, 一些特性会被分组发布. 为了识别每次发布, 应该为可交付软件添加一个唯一的版本号. 这是通过为项目属性 version 设置一个字符串来实现的. 在简单的用例中非常适合使用 String 类型的数据, 但是如果你想要知道项目确切的次版本号该怎么办呢 ?

你可以轻松地使用类的属性来设置、检索和修改编号方案的特定部分. 你可以更进一步的通过外部化版本信息来持久化数据存储, 比如一个文件或数据库, 可以避免通过修改构建脚本本身来改变项目的版本. 下图展示了构建脚本之间的交互，属性文件中包含了版本信息和数据表示类。在接下来的章节里，你会创建和学习如何使用所有的这些文件。

![001](/Users/sherlock/Desktop/notes/allPics/Gradle/001.jpg)

上图在构建脚本运行阶段，从属性文件中读取项目版本。ProjectVersion数据类被实例化。每个版本类别都被转换成数据类的属性值。ProjectVersion实例被赋值给项目的version属性。

要自动化项目生命周期，那么以编程方式控制版本管理方案将变得很有必要。

举一个例子：你的代码已经通过了所有的功能测试并且准备组装发布。项目的当前版本是1.2-SNAPSHOT。在构建最后的WAR文件之前，你要把它变成一个发布版本1.2，并且自动部署到生产服务器上。这些步骤都可以通过创建 task 的方式进行建模：一个用于修改项目版本，一个用于部署WAR文件。让我们更进一步来学习关于 task 的知识，以实现灵活的项目版本管理。



### 4.2.2 声明 task 动作

动作（action）就是在 task 中合适的地方放置构建逻辑。Task 接口提供了两个相关的方法来声明task动作：doFirst（Closure）和 doLast（Closure）。当task被执行的时候，动作逻辑被定义为闭包参数被依次执行。
通过添加一个task printVersion来简单地开始吧。这个task的作用就是打印出当前的项目版本。把这段逻辑放在doLast方法里面，如下面代码片段所示：

```groovy
version '1.0-SNAPSHOT'

task printVersion {
	doLast {
		println "Version: $version"
	}
}
```

当执行gradle printVersion命令时，你会看到正确的版本号：

```sh
➜  ToDoApp git:(master) ✗ gradlew printVersion 
> Task :printVersion
Version: 1.0-SNAPSHOT
```

如果把这段逻辑放到doFirst方法里面，也会得到同样的结果.

#### 给现有task添加动作

到目前为止，你只给task printVersion添加了一个动作，或者是第一个动作或者是最后一个动作。但并不限于为每个task只添加一个动作。事实上，在task创建后，你可以根据需要添加很多动作。在内部，每个task都保持了一个动作列表。在运行时，它们按顺序执行。让我们看一个示例task的修改后版本：

```groovy
task printVersion {
	doFirst {
		println "Before reading the project version"
	}
	
	doLast {
		println "Version: $version"
	}
}
// 在动作列表的开始和结束添加闭包
printVersion.doFirst {
	println "First action"
}
printVersion.doLast {
	println "Last action"
}
```

上述方法执行结果为:

```sh
➜  ToDoApp git:(master) ✗ gradlew -q printVersion
First action
Before reading the project version
Version: 1.0-SNAPSHOT
Last action
```

我们可以给现有的task添加一些动作。这在你想要为不是自己编写的task执行自定义逻辑时非常有用。比如，为Java插件的compileJava task添加一个doFirst 动作来检查项目中至少包含一个Java源文件。



### 4.2.3 访问 DefaultTask 属性

接下来，你将改进输出版本号的方式。Gradle提供了一个基于SLF4J日志库的logger实现。除了实现常规范围的日志级别（DEBUG、ERROR、INFO、TRACE、WARN）之外，Gradle还增加了一些额外的日志级别。通过Task的方法可以直接访问logger实例。现在，要打印 QUIET 日志级别的版本号:

```groovy
task printVersion {
	doLast {
		logger.quiet "Version: $version"
	}
}
```

由此可见，访问task的属性是多么容易呀！其实还有两个属性：group和description。它们都是task文档的一部分。
description属性用于描述任务的作用，而group属性则用于定义task的逻辑分组。在创建task的时候，为这两个属性设置值作为参数：

```grooy
task printVersion(group: 'Versioning',
        description: 'Prints project version.') {

    doLast {
        logger.quiet "Version: $version"
    }
}
```

或者，也可以通过调用setter方法来设置属性：

```groovy
task printVersion {
	group = 'Versioning'
	description = 'Prints project version.'
  doLast {
  	logger.quiet "Version: $version"
  }
}
```

当运行gradle tasks时，我们可以看到task正确的分组和描述：

```sh
➜  ToDoApp git:(master) ✗ gradlew tasks          

> Task :tasks

------------------------------------------------------------
Tasks runnable from root project
------------------------------------------------------------
......

Versioning tasks
----------------
printVersion - Prints project version.

......

To see all tasks and more detail, run gradlew tasks --all

To see more detail about a task, run gradlew help --task <task>

BUILD SUCCESSFUL in 4s
1 actionable task: 1 executed

```

尽管设置task的描述和分组是可选的，但是为所有的task指定值是一个好主意。这会帮助最终用户比较容易地去识别task的功能。接下来，我们将回顾一下task之间定义依赖关系的复杂性。



### 4.2.4 定义 task 依赖

dependsOn方法允许声明依赖一个或多个task。你已经看到Java插件充分利用了这个概念，通过创建task依赖关系图来建模完整的task生命周期如build task。下面清单显示了使用dependsOn方法应用task依赖的不同方式。

```groovy
task first {
    doLast {
        println "first"
    }
}

task second {
    doLast {
        println "second"
    }
}

task printVersion(dependsOn: [second, first]) {

    group = 'Versioning'
    description = 'Prints project version.'
    
    doLast {
        logger.quiet "Version: $version"
    }
}

task third {
    doLast {
        println "third"
    }
}

third.dependsOn('printVersion')
```

在命令行通过调用task third来执行依赖链上的其他task：

```sh
➜  ToDoApp git:(master) ✗ gradlew third
> Task :first
first

> Task :second
second

> Task :printVersion
First action
Version: 1.0-SNAPSHOT
Last action

> Task :third
third

BUILD SUCCESSFUL in 6s
4 actionable tasks: 4 executed
```

如果仔细看看task的执行顺序，你可能会对结果感到惊讶。task printVersion声明了依赖first和second task。难道你不期望second在first执行之前得到执行？在Gradle中，task执行顺序是不确定的。

#### task依赖的执行顺序

理解 "Gradle 并不能保证 task 依赖的执行顺序" 是很重要的。dependsOn方法只是定义了所依赖的task需要先执行。Gradle的思想是声明在一个给定的task执行之前什么该被执行，而没有定义它该如何执行。如果你之前使用过像Ant这种命令式地定义依赖的构建工具的话，那么Gradle的这个概念就有点难以理解了。在本章的后续部分你将看到，在Gradle中，执行顺序是由task的输入/输出规范自动确定的。这种构建设计有很多好处。一方面，你不需要知道整个task依赖链上的关系是否发生改变，这样可以提高代码的可维护性和避免潜在的破坏。另一方面，因为构建没有严格的执行顺序，也就是支持task的并行执行，这样可以极大地节约构建执行时间。



### 4.2.5 终结器 task

在实践中，你会发现所依赖的task执行后需要清理某种资源。一个典型的例子就是Web容器需要对已经部署的应用程序运行集成测试。针对这种情景Gradle提供了终结器task（finalizer task），即使终结器task失败了，Gradle的task也会按预期运行。下面的代码片段展示了如何通过使用Task方法finalizedBy来使用一个特定的终结器task。

```groovy
task first {
    doLast {
        println "first"
    }
}

task second {
    doLast {
        println "second"
    }
}

first.finalizedBy second
```

你会看到执行first会自动触发second：

```sh
➜  ToDoApp git:(master) ✗ gradlew first

> Task :first
first

> Task :second
second

BUILD SUCCESSFUL in 3s
2 actionable tasks: 2 executed
```

第7章将通过真实的例子更深入地介绍终结器task的概念。在下一节中，将编写一个Groovy类来细粒度地控制版本管理方案。



### 4.2.6 添加任意代码

现在是时候来讨论在Gradle构建脚本中定义通用的Groovy代码的功能了。在实践中，你可以在Groovy脚本或类中以习惯的方式来编写类和方法了。在本节中，你将创建一个表示项目版本的类。在Java中，遵循bean惯例的类被称为POJO。根据定义，它们通过getter和setter方法来暴露属性。随着时间的推移，手工编写这些方法会变得非常烦琐。Groovy也有等效的POJO，即POGO，只是需要声明属性，而不需要设置访问权限修饰符。它们的getter和setter方法本质上是在生成字节码时自动添加的，因此在运行时它们可以直接被使用。在下面的清单中，指定了一个POGO实例ProjectVersion。实际的值是在构造器中设置的。

```groovy
version new ProjectVersion(0, 1)

class ProjectVersion {

    Integer major
    Integer minor
    Boolean release

    ProjectVersion (Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    @Override
    String toString() {
        "$major.$minor${release ? '': '-SNAPSHOT'}"
    }
}
```

运行修改后的构建脚本，你会看到printVersion task产生的结果与之前的完全相同。遗憾的是，你仍然需要手动编辑构建脚本来改变版本类别。接下来，我们将版本信息存储在外部文件中并配置构建脚本来读取它。



### 4.2.7 理解 task 配置

在开始编写代码之前，你需要创建一个名为version.properties的属性文件，并且为每一个版本的类别如主版本和次版本设置不同的属性。下面的键值对表示最初的版本0.1-SNAPSHOT：

```properties
major = 0
minor = 1
release = false
```

#### 添加task配置块

清单4.3声明了一个名为loadVersion的task，用于从属性文件中读取版本类别，并将新创建的ProjectVersion实例赋值“给项目的版本属性。乍一看，这个task很像你之前定义的其他task。但是如果仔细观察，就会发现你没有定义动作或者使用左移操作符（<<）。Gradle称之为task配置。

> task 的方法体中的没有定义在 action 中的代码块被称为 task 配置

```groovy
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion()
}

ProjectVersion readVersion() {
    logger.quiet('Reading the version file')

    if (!project.versionFile.exists()) {
        throw new GradleException("Require version file doesn't exists: $versionFile.canonicalPath")
    }
    Properties versionProps = new Properties()
    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }
    new ProjectVersion(versionProps.major.toInteger(),
            versionProps.minor.toInteger(),
            versionProps.release.toBoolean()
    )
}
```

现在运行printVersion，你会看到新创建的loadVersion先被执行。尽管task的名字没有被打印出来，但是你仍然知道这种情况，因为打印的日志是你自己添加的。

```sh
➜  gradle_lecture gradlew pV   
*************************************************************
Dump gradle information
Version:5.6.4
GradleUserHomeDir:/Users/sherlock/.gradle
GradleHomeDir:/Users/sherlock/.gradle/wrapper/dists/gradle-5.6.4-bin/bxirm19lnfz6nurbatndyydux/gradle-5.6.4
*************************************************************

> Configure project :
Reading the version file
printVersion: 3.3-SNAPSHOT

> Task :printVersion
First Action
Last Action

BUILD SUCCESSFUL in 1s
1 actionable task: 1 executed
```

你也许会问自己，为什么这个task被完全调用了？当然，你没有声明依赖关系，也没有在命令行调用它。原因就是task配置块永远在task动作执行之前被执行。完全理解这种行为是了解Gradle构建生命周期的关键。让我们具体看看每一个构建阶段。

#### Gradle构建生命周期阶段

***无论什么时候执行Gradle构建，都会运行三个不同的生命周期阶段：初始化、配置和执行。***

下图可视化了构建阶段的运行顺序和它们执行的代码。![002](/Users/sherlock/Desktop/notes/allPics/Gradle/002.jpg)

在初始化阶段，Gradle为项目创建了一个Project实例。在给定的构建脚本中只定义了一个项目。在多项目构建中，这个构建阶段变得更加重要。根据你正在执行的项目，Gradle找出哪些项目依赖需要参与到构建中。注意，在这个构建阶段当前已有的构建脚本代码都不会被执行。在第6章中，当你模块化To Do应用程序使其成为多项目构建的时候，这种情况会发生变化。

初始化阶段后面紧接着的是配置阶段。Gradle构造了一个模型来表示任务，并参与到构建中来。增量式构建特性决定了模型中的task是否需要被运行。这个阶段非常适合于为项目或指定task设置所需的配置。

注意：项目每一次构建的任何配置代码都可以被执行——即使你只执行gradle tasks。

在执行阶段，所有的task都应该以正确的顺序被执行。执行顺序是由它们的依赖决定的。如果任务被认为没有修改过，将被跳过。比如，如果task B依赖于task A，那么当在命令行运行gradle B时执行顺序将是A->B。

正如你所见，Gradle的增量式构建特性紧紧地与生命周期相结合。在第3章中，你已经看到Java插件大量地使用了这个特性。如果Java源文件与最后一次运行的构建不同的话，则只运行compileJava task。最终，这个特性将充分提高构建的性能。在下一节中，我们将介绍针对自己的task如何使用增量式构建特性。



### 4.2.8 声明task的inputs和outputs

Gradle通过比较两个构建task的 inputs 和 outputs 来决定task是否是最新的，如图4.6所示。自从最后一个task执行以来，如果 inputs 和 outputs 没有发生变化，则认为task是最新的。因此，只有当inputs和outputs不同时，task才运行；否则将跳过。

输入可以是一个目录、一个或多个文件，或者是一个任意属性。一个task的输出是通过一个目录或1～n个文件来定义的。inputs和outputs在DefaultTask类中被定义为属性或者有一个直接类来表示，如下图所示。![003](/Users/sherlock/Desktop/notes/allPics/Gradle/003.jpg)



我们来看看这个特性。假设你想创建一个task，为产品发布准备项目的可交付版本。为此，你想将项目版本从SNAPSHOT改变为release。下面的清单定义了一个新的task，将Boolean值true赋值给版本属性release。这个task也将版本变化传递到属性文件中。
清单4.4 将项目版本切换为产品发布版本

```groovy
task makeReleaseVersion {
    group = 'versioning'
    description = 'Makes project a release version.'

    doLast {
        version.release = true
        ant.propertyfile(file: versionFile) {
            entry (key: 'release', type: 'string', operation: '=', value: 'true')
        }
    }
}
```

正如所期望的，运行这个task将改变版本属性，并将新值持续保存到属性文件中。下面的输出显示了这个行为：

```sh
➜  gradle_lecture gradlew mRV

> Configure project :
Reading the version file
printVersion: 3.3

BUILD SUCCESSFUL in 1s
1 actionable task: 1 executed
```



makeReleaseVersion task可能是另一个生命周期task的一部分，用来将WAR文件部署到生产服务器上。令人感觉痛苦的是部署出错了。可能是网络出现故障，导致无法联系上服务器。修复网络故障后，需要再次运行部署task。因为makeReleaseVersion task被声明为依赖部署task，所以它会自动重新运行。注意，虽然我们将项目版本标记为产品发布版本，但是Gradle并不知道。为了解决这一问题，需要声明它的inputs和outputs，如下面的清单所示。

```groovy
task makeReleaseVersion {
    group = 'versioning'
    description = 'Makes project a release version.'

    inputs.property('release', versionFile.release)
    outputs.file versionFile
  
    doLast {
        version.release = true
        ant.propertyfile(file: versionFile) {
            entry (key: 'release', type: 'string', operation: '=', value: 'true')
        }
    }
```

把你想执行的代码放到doLast动作闭包中，并从task声明中移除左移操作符。这样，你就清晰地分离了它的配置和动作代码。

#### task的inputs和outputs的执行

***记住，task的inputs和outputs是在配置阶段执行的用来连接task依赖。这就是它们需要在配置块中被定义的原因。***为了避免出现所不期望的行为，请确保赋给inputs和outputs的值在配置期间是可访问的。如果你需要通过编程获得输出，则可以通过TaskOutputs上的upToDateWhen（Closure）方法来实现。与常规的inputs和outputs相比，这个方法是在执行期间执行的。如果闭包返回true，这个task则会被认为是最新的。
现在，如果你执行task两次，就会发现Gradle已经知道项目版本被设置为发布版本，而且自动跳过task的第二次执行。

如果你没有手动修改属性文件中的release属性，那么makeReleaseVersion将被标记为最新的，永远会被跳过执行。
至此，你已经使用Gradle的DSL在构建脚本中创建和修改了task。每个task都得到在Gradle的配置阶段实例化的task对象支持。在大多数情况下，使用简单的task就可以完成工作。然而，有时候你可能想要完全控制task实现。在下一节中，你将以自定义task实现的方式来重写makeReleaseVersion task。



### 4.2.9 编写和使用自定义 task

makeReleaseVersiontask 中的动作逻辑相当简单。当前代码的可维护性显然不是问题。然而，当你工作在多项目中时，就会发现原本简单的task随着项目规模的快速增长需要添加更多的逻辑。此时就有了对类和方法中代码的结构化需求。你应该能够应用像在常规的产品源代码中习惯使用的编码惯例。Gradle不建议你用某种特殊方式去编写task。你可以完全控制构建源代码。你所选择的编程语言如Java、Groovy或者其他基于JVM的语言，完全取决于你对它们的熟悉程度。

自定义task包含两个组件：自定义的task类，封装了逻辑行为，也被称作任务类型，以及真实的task，提供了用于配置行为的task类所暴露的属性值。Gradle把这些task称为增强的task。
可维护性是编写自定义task类的优势之一。因为你正在操作一个实际的类，通过单元测试任何方法都是完全可测试的。测试构建代码不在本章讨论范围之内。如果你想进一步了解，请跳到第7章。与简单的task相比，增强的task的另一个优势是可重用性。自定义task所暴露的属性可以在构建脚本中进行单独设置。记住了增强的task的诸多好处，让我们讨论一下如何编写自定义的task类。

#### 编写自定义的 task 类

正如本章前面所提到的，Gradle为构建脚本中的每个简单的task都创建了一个DefaultTask类型的实例。当创建一个自定义task时，你需要做的是，创建一个继承DefaultTask的类。下面的清单展示了如何将makeReleaseVersion的逻辑表示为使用Groovy编写的自定义类ReleaseVersionTask。

```java
class ReleaseVersionTask extends DefaultTask {

    @Input Boolean release
    @OutputFile File destFile

    ReleaseVersionTask() {
        group = 'versioning'
        description = 'Make project a release version.'
    }

    @TaskAction
    void start() {
        project.version.release = true
        ant.propertyfile(file: destFile) {
            entry (key: 'release', type: 'string', operation: '=', value: 'true')
        }
    }
}
```

在这个清单中，没有使用DefaultTask的属性来声明它的输入和输出。而是使用org.gradle.api.tasks包下的注解。

#### 通过注解表示输入和输出

task的输入和输出注解为你的实现添加了语义糖。它们不仅与TaskInputs和TaskOutputs方法有相同的效果，而且它们还能够充当自动文档。稍微一看，你就会知道什么数据会被当作输入，该task会生成什么样的输出工件。探索这个包的Javadocs文档时，你会发现Gradle提供了各种各样的注解。
在自定义的task类中，你可以使用@Input注解声明输入属性release，使用@OutputFile注解定义输出文件。为属性添加输入和输出注解并不是唯一的选择，你也可以为getter方法添加注解。

#### task 输入验证

@Input注解会在配置期间验证属性值。如果值为null,Gradle会抛出TaskValidationException异常。为了允许输入为null值，我们需要给它添加@Optional注解

#### 使用自定义task

通过创建一个动作方法和暴露它的配置属性，你实现了一个自定义的task类。但你会怎么使用它呢？在构建脚本中，你需要创建一个ReleaseVersionTask类型的task，并且通过为它的属性赋值来设置输入和输出，如下面的清单所示。认为这是创建一个特定类的新实例，并且在构造器中为它的属性设置值。

```java
// 定义一个增强的 ReleaseVersionTask 类型的 Task
task makeReleaseVersion(type: ReleaseVersionTask) {
    release = version.release
    destFile = versionFile
}
```

正如所期望的，增强的makeReleaseVersion task与简单的task的运行结果表现完全一致。与简单的task实现相比，增强的task的一个巨大优势在于所暴露的属性可以被单独赋值。

### 4.2.10 Gradle的内置task类型

你还记得上一次手动部署产品出错的情形吗？我想你的脑海里依然会有这样生动的画面：愤怒的客户把电话打到了你的支持团队，老板敲着你的桌子问怎么回事，当时你和同事正疯狂地试图找出启动程序时堆栈跟踪抛出异常的根源。在手动发布过程中忘记一个简单的步骤就可能是致命的。

我们是专业人士，要以自动化构建生命周期的每一方面为傲。以自动化方式修改项目的版本管理方案只是建模发布过程的第一步。为了能够快速地从失败的部署中恢复过来，一个好的回滚策略是必需的。保留一份最新的稳定的应用程序可交付版本的备份非常重要。你可以为To Do应用程序使用Gradle封装的一些task类型来实现这个过程的各部分。

这就是你所需要做的。在将代码部署到产品环境中之前，你需要创建一个发布包。把它作为将来项目部署失败时回退的可交付版本。发布包是一个ZIP文件，包括了Web应用程序存档、所有的源文件和版本属性文件。创建发布包后，文件被复制到备份服务器上。备份服务器可以通过一个挂载的共享驱动器来访问或者通过FTP传输文件。因为我不想使这个实例过于复杂，所以你只需要将其复制到build/backup子目录中就行。图4.8展示了你想要执行的task的顺序。

![004](/Users/sherlock/Desktop/notes/allPics/Gradle/004.jpg)



#### 使用 task 类型

Gradle的内置task类型都是DefaultTask的派生类。因此，它们可以被构建脚本中的增强的task使用。Gradle提供了广泛的task类型，但是在这个例子中只使用两个。下面的清单显示了在产品发布过程中用到的task类型Zip和Copy。你可以在DSL指南中找到完整的task参考。

```java
task createDistribution(type: Zip, dependsOn: makeReleaseVersion) {
    // 隐式的引出 war task 的输出
    from war.outputs.files
    // 将所有源文件都放到 zip 文件的 src 目录下
    from(sourceSets*.allSource) {
        into 'src'
    }
    // 为 zip 文件添加版本文件
    from(rootDir) {
        include versionFile.name
    }
}

task backupReleaseDistribution (type: Copy) {
    // 隐式的引出 createDistribution task 的输出
    from createDistribution.outputs.files

    into "$buildDir/backup"
}

task release(dependsOn: backupReleaseDistribution) {
    logger.quiet "Releasing the project......"
}
```

在这个清单中，使用了不同的方式来告诉Zip和Copy task包括哪些文件以及它们被放到哪里了。这里使用的许多方法都继承自父类AbstractCopyTask，如图4.9所示。想了解全部可用的task类型，请参考类的Javadocs文档。

task类型Zip和Copy的继承关系

![Image00131](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00131.jpg)



你使用的task类型所提供的配置选项远多于这个实例中所显示的。同样的，想了解全部可用的配置选项，请参考DSL参考手册或者Javadocs文档。接下来，我们会深入地看看它们的task依赖。

#### task 依赖推断

你可能已经注意到，清单中的两个task之间的依赖关系是通过dependsOn方法显式声明的。然而，一些task并不直接依赖其他task（比如，createDistribution对于war）。Gradle是如何知道事先执行依赖的task呢？其实通过使用一个task的输出作为另一个task的输入，Gradle就可以推断出依赖关系了。因此，所依赖的task会自动运行。让我们来看看完整的task执行图：

```
// PS: gradle <task_name> 无法显示一个任务执行过程中它所依赖任务名, 我这里使用了 -m 虚执行(实际并未执行任务)参数来查看任务 release 的依赖任务.也可以通过 IDEA 来查看依赖.

➜  gradle_lecture gradlew -m release        

> Configure project :
Releasing the project......
Reading the version file
printVersion: 1.10
:makeReleaseVersion SKIPPED
:compileJava SKIPPED
:processResources SKIPPED
:classes SKIPPED
:war SKIPPED
:createDistribution SKIPPED
:backupReleaseDistribution SKIPPED
:release SKIPPED

BUILD SUCCESSFUL in 888ms
```

运行构建后，你可以在build/distributions目录下找到所生成的ZIP文件，这个目录是用于归档task的默认输出目录。通过设置destinationDir属性你可以很容易地指定一个不同的发布包输出目录。下面的目录树显示了构建所生成的相关工件：

![Image00133](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00133.jpg)

增量式构建支持内置的task类型。在不改变任何源文件的情况下，连续多次运行的task会被标记为最新的。接下来，你将了解到如何定义一个task的行为取决于灵活的task名称。



4.2.11 task 规则

有时候你可能会发现在某些情况下，自己所编写的多个task却做着类似的事情。例如，你想通过两个task扩展版本管理功能：一个用来增加项目的主版本，另一个对于次版本类别做同样的事情。假定这两个task都会将变化持续保存到版本文件中。如果你比较下面清单中这两个task的doLast行为，就会发现主要是复制了代码并应用了次版本的变化。

```java
task incrementMajorVersion {
    group = 'versioning'
    description = 'Increments project major version.'

    doLast {
        String currentVersion = version.toString()
        ++version.major
        String newVersion = version.toString()
        logger.quiet "Incrementing major project version: $currentVersion -> $newVersion"

        ant.propertyfile(file: versionFile) {
            entry(key: 'major', type: 'int', operation: '+', value: 1)
        }
    }
}

task incrementMinorVersion {
    group = 'versioning'
    description = 'Increments project minor version.'

    doLast {
        String currentVersion = version.toString()
        ++version.minor
        String newVersion = version.toString()
        logger.quiet "Incrementing minor project version: $currentVersion -> $newVersion"

        ant.propertyfile(file: versionFile) {
            entry(key: 'minor', type: 'int', operation: '+', value: 1)
        }
    }
}
```

如果你在一个版本为0.1-SNAPSHOT的项目上运行gradle incrementMajonVersion，将看到版本升到1.1-SNAPSHOT。在INTO日志级别运行将会看到更详细的输出信息

```
➜  gradle_lecture gradlew -q incrementMajorVersion

Reading the version file
Releasing the project......
printVersion: 1.0
Incrementing major project version: 2.0 -> 1.0
```

两个独立的task工作得很好，但是你肯定可以改进这个实现。最后，你会对维护重复的代码失去兴趣。



#### task 规则命名模式

Gradle 也引入了 task 规则的概念, 根据 task名称模式执行特定的逻辑. 该模式由两部分组成：task名称的静态部分和一个占位符。它们联合起来就组成了一个动态的task名称。如果你想将task规则应用于前面的例子中，那么命名模式看起来应该像这样：increment\<Classifier>Version。在命令行上执行这个task，将以“骆峰命名法”（camel-case）指定类别占位符（比如，incrementMajorVersion或incrementMinorVersion）。

#### task 规则实践

Gradle的一些核心插件充分地利用了task规则。Java插件定义的task规则之一是clean\<TaskName>，用来删除指定task的输出。比如，在命令行运行gradle cleanCompileJava会删除所有产品代码的class文件。

#### 声明 task 规则

你只是了解了给task规则定义一个命名模式，***但是在构建脚本中应该如何声明一个task规则呢？***为了给项目添加task规则，首先你需要获得对TaskContainer的引用。一旦有了这个引用，你就可以调用addRule（String,Closure）方法了。第一个参数提供了描述信息（比如，task命名模式），第二个参数声明了要执行的闭包来应用规则。遗憾的是，不能像简单的task一样直接通过Project的方法来创建task规则，如下图所示。

![Image00137](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00137.jpg)

简单的task可以直接通过调用project实例的方法来添加。task规则只能通过task容器来添加，所以你首先需要通过调用getTasks（）方法来获取对它的引用。

对如何为项目添加task规则有了一个基本的了解后，你可以开始编写实际的闭包实现了。下面的清单演示了如何应用task规则使其变成富有表达性的工具来实现具有相似逻辑的task动作。

```java
// 方法参数为该 task 规则的描述信息.
tasks.addRule("Pattern: increment<Classifier>Version - Increments the project version classifier.") { String taskName ->
    if (taskName.startsWith('increment') && taskName.endsWith('Version')) {
            // 给符合命名模式的 task 动态添加一个 doLast 动作
            task(taskName) {
                doLast {
                    // 这个语法很奇妙, 对 String 做减法, 例如 'ABC' - 'A' - 'C' = 'B'
                    String classifier = (taskName - 'increment' - 'Version').toLowerCase()
                    String currentV = version.toString()

                    switch (classifier) {
                        case 'major':
                            ++ version.major
                            break
                        case 'minor':
                            ++ version.minor
                            break
                        default:
                            throw new GradleException("Invalid version type $classifier. Allowed types: ['Major', 'Minor']'")
                    }

                    String newV = version.toString()
                    logger.quiet "Incrementing $classifier project version: $currentV -> $newV"

                    ant.propertyfile(file: versionFile) {
                        entry(key: classifier, type: 'int', operation: '+', value: 1)
                    }
                }
            }
        }
}
```

在项目中添加task规则之后，你会发现当运行帮助task tasks时会列出一个具体的task组Rules：

```
➜  gradle_lecture gradlew tasks                    


> Task :tasks

......


Rules
-----
Pattern: clean<TaskName>: Cleans the output files of a task.
Pattern: build<ConfigurationName>: Assembles the artifacts of a configuration.
Pattern: upload<ConfigurationName>: Assembles and uploads the artifacts belonging to a configuration.
Pattern: increment<Classifier>Version: Increments the project version classifier.

To see all tasks and more detail, run gradlew tasks --all

To see more detail about a task, run gradlew help --task <task>

BUILD SUCCESSFUL in 1s
1 actionable task: 1 executed
```

task规则不能像处理任何其他的简单的task或增强的task一样被独立分组。task规则即使通过插件声明了，它也将永远显示在Rules组下。



### 4.2.12 在 buildSrc 目录下构建代码

你已经看到了如何快速地构建脚本代码。在这一章中，你已经在构建脚本中创建了两个Groovy类：ProjectVersion和自定义task ReleaseVersionTask。这些类最适合被移动到项目的buildSrc目录下。将构建代码放在buildSrc目录下是一个良好的软件开发实践。你可以应用在其他项目中习惯使用的方式来结构化代码，甚至是编写测试代码。

Gradle在buildSrc目录下使源文件结构标准化。Java代码需要放在src/main/java目录下，Groovy代码则放在src/main/groovy目录下。位于这些目录下的代码都会被自动编译，并且被加入到Gradle构建脚本的classpath中。buildSrc目录是组织代码的最佳方式。因为要处理这些类，所以也可以把它们放到指定的包下。可以让它们成为com.manning.gia包的一部分。下面的目录结构显示了Groovy类的存放位置：

![Image00140](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00140.jpg)

记住，提取类放到源文件中需要一些额外的工作。在构建脚本中定义一个类和一个独立的源文件的区别在于你需要从Gradle API导入类。下面的代码片段显示了包和导入的自定义task ReleaseVersionTask的声明：

```java
package cgs.gradle

import org.gradle.api.DefaultTask
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.OutputFile
import org.gradle.api.tasks.TaskAction

class ReleaseVersionTask extends DefaultTask {
	......
}
```

相应地，构建脚本需要从buildSrc导入编译过的类（比如，com.manning.gia.ReleaseVersionTask）。下面的控制台输出显示了在命令行调用这个task之前运行编译task：

```
> Task :buildSrc:compileJava NO-SOURCE
> Task :buildSrc:compileGroovy
> Task :buildSrc:processResources NO-SOURCE
> Task :buildSrc:classes
> Task :buildSrc:jar
> Task :buildSrc:assemble
> Task :buildSrc:compileTestJava NO-SOURCE
> Task :buildSrc:compileTestGroovy NO-SOURCE
> Task :buildSrc:processTestResources NO-SOURCE
> Task :buildSrc:testClasses UP-TO-DATE
> Task :buildSrc:test NO-SOURCE
> Task :buildSrc:check UP-TO-DATE
> Task :buildSrc:build

> Configure project :
Reading the version file
Releasing the project......
printVersion: 5.2

> Task :makeReleaseVersion UP-TO-DATE

BUILD SUCCESSFUL in 18s
```

buildSrc目录被视为Gradle项目的指定路径。由于没有编写任何单元测试，所以跳过了编译和执行task。第7章将完全致力于在buildSrc中编写测试类。

在前面章节中，你了解了简单的task工作的底细、自定义的task类和Gradle API提供的特定的task类型。我们学习了task动作和配置代码的区别，以及它们相应的用例，其中一个要点是task动作和配置代码在构建生命周期的不同阶段执行。本章后续部分将讨论当特定的生命周期事件被触发时如何编写执行代码。



## 4.3 挂接到构建生命周期

作为一个构建脚本开发人员，不能仅限于编写在不同的构建阶段执行的task动作或者配置逻辑。有时候当一个特定的生命周期事件发生时你可能想要执行代码。一个生命周期事件可能发生在某个构建阶段之前、期间或者之后。在执行阶段之后发生的生命周期事件是构建的完成。
假设你想在开发周期中尽可能早地获得失败构建的反馈信息。对失败构建一个典型的反应是发送邮件给团队中的所有开发人员，以使代码恢复正常。有两种方式可以编写回调生命周期事件：在闭包中，或者是通过Gradle API所提供的监听器接口实现。Gradle不会引导你采用哪种方式去监听生命周期事件，这完全取决于你的选择。采用监听器实现最大的优势在于你处理的类通过编写单元测试是完全可测试的。下面为你提供一个有用的生命周期钩子（hook）的想法，如下图所示。

![Image00144](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00144.jpg)

所有可用的生命周期钩子扩展列表超出了本书的讨论范围。许多生命周期回调方法被定义在Project和Gradle接口中。Gradle的Javadocs为你的用例提供了合适的回调方法。

不要害怕使用生命周期钩子。它们不是Gradle API的秘密后门。相反，它们是特意提供的，因为Gradle无法预测企业级构建的需求。

在接下来的两节中，我将演示task执行图生成以后如何立即接收通知。为了完全理解构建task执行图时在底层发生了什么，我们首先来看看Gradle内部是如何运作的。

#### Task执行图的内部展示

在配置时，Gradle决定了在执行阶段要运行的task的顺序。正如第1章所提到的，表示依赖关系的内部结构被建模为一个有向无环图（DAG）。图中的每个task被称为一个节点，并且每个节点通过有向边连接起来。你最有可能通过声明dependsOn关系或者通过利用隐式task依赖干预机制来创建这些节点之间的连接。重要的是要注意DAG没有一个闭环。换句话说，一个先前执行的task将永远不会被再次执行。下图展示了早期发布过程建模的DAG表示。

![Image00145](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00145.jpg)

现在，对task图的内部表示有了更好的了解，你可以在构建脚本中编写一些代码来应对它了。



### 4.3.1 挂接到 task 执行图

回想一下你所实现的makeReleaseVersion task，它作为release task的依赖被自动执行。除了编写一个task改变项目版本来指示产品准备发布外，你也可以通过编写一个生命周期钩子来实现同样的目标。因为构建确切地知道task在得到执行之前哪些将参与构建，你可以查询task图来检查它的存在。下图显示了访问task执行图的相关接口及其方法。

![Image00146](/Users/sherlock/Desktop/notes/allPics/Gradle/Image00146.jpg)

当task执行图生成完成后，TaskExecutionGraph提供的whenReady方法会被调用

接下来你将在适当的地方放置生命周期钩子。在上图中通过调用whenReady方法注册一个闭包扩展了构建脚本，task图生成完成后，它将立即被执行。因为你知道在执行图中任何task之前会运行逻辑行为，所以可以完全删除makeReleaseVersion task和省略createDistribution中的dependsOn声明。



### 4.3.2 实现 task 执行图监听器



### 4.3.3 初始化构建环境 

假设你想要在构建完成后获得构建结果。当构建完成后，你想知道构建成功了还是失败了。你也希望能够确定执行了多少task。Gradle的核心插件之一，build-announcements插件，提供了一种将通告发送到本地通知系统如Snarl（Windows）或Growl（Mac OS X）的方式。这个插件根据你的操作系统自动选择正确的通知系统。如图4.15所示为Growl显示的通知。

你可以把这个插件独立地运用到每个项目上，但是为什么不使用Gradle提供的强大机制呢？初始化脚本会在任何构建脚本逻辑解析执行之前运行。编写一个初始化脚本把插件应用到项目中，不需要人工干预。在<USER_HOME>/.gradle/init.d下创建初始化脚本，如下面的目录树所示：

```
gradle.projectsLoaded {Gradle gradle ->
	gradle.rootProject {
		apply plugin: 'build-announcements'
	}
}
```

请注意，一些生命周期事件只有在适当的位置声明才会发生。比如，如果将生命周期挂接闭包Gradle#projectsLoaded（Closure）声明在build.gradle文件中，那么将不会发生这个事件，因为项目创建发生在初始化阶段。



## 4.4 总结

每个Gradle构建脚本都包含两个基本的构建块：一个或多个项目（project）和任务（task）。这两个元素都与Gradle的API密切相关，并且它都有一个直接类表示。在运行时，Gradle会通过构建定义创建一个模型，将它存储在内存中，并且可以通过方法访问。你了解到属性是控制构建动作的一种方式。项目暴露了标准的属性。除此之外，你也可以在Gradle的领域模型对象上定义额外的属性（比如，在项目和任务级别）来声明任意的用户数据。

在本章后部分，你了解了task的底细。作为一个例了，你实现了构建逻辑来控制存储在外部属性文件中的项目版本编号方案。你首先将简单的task添加到构建脚本中。构建逻辑可以被直接定义在task的动作闭包中。每个task都继承自org.gradle.api.DefaultTask类。因此，它通过父类的方法加载了很多功能。

对初学者而言，理解构建生命周期及其阶段执行顺序是至关重要的。***Gradle 明确区分了task动作和task配置。task动作，通过闭包doFirst和doLast，在执行阶段运行。那些不在task动作中定义的代码被认为是配置，因此在配置阶段提前执行。***

接下来，我们将注意力转向实现非功能性需求：构建执行性能、代码可维护性和可重用性上。通过声明输入和输出数据，对已有的task实现添加增量式构建支持。***在初始的和后续的构建task之间如果输入数据没有发生变化，那么执行将被跳过。***实现增量式构建支持其实非常简单和容易。如果做得正确，它可以极大地减少构建的执行时间。复杂的构建逻辑最好在自定义的task类中实现结构化，这可以给你带来面向对象编程的所有好处。你练习编写了一个自定义的task类，用来将现有的逻辑移植到 DefaultTask实现中。通过移动可编译的代码，你也可以清理buildSrc目录下的构建脚本。Gradle提供了广泛的可重用的task类型如Zip和Copy。通过建模一个task依赖关系链合并这两种类型来发布项目。

访问Gradle的内部并不局限于模型。***你可以注册构建生命周期钩子（hook），当触发目标事件时来执行代码。***作为一个例子，通过闭包和监听器实现，你编写了task执行图生命周期钩子。通过初始化脚本可以应用公共代码，比如构建中的生命周期监听器。

你已经初步了解了声明对外部类库的依赖机制。在下一章中，我们将深入讨论如何处理依赖关系，以及依赖冲突到底是如何解决的。

